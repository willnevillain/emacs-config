#+TITLE: Emacs Config
#+AUTHOR: willnevillain
#+OPTIONS: toc:nil num:nil

* Required external dependencies

For this configuration to work correctly, the following dependencies must be installed on your system.

- =ripgrep= for fast searching.
- =FiraCode Nerd Font= font.

* SSL config

Allows for pulling from =https= package archives.

#+begin_src emacs-lisp :results output silent
  (setq tls-checktrust t)
  (setq gnutls-verify-error t)
  (setq gnutls-algorithm-priority "NORMAL:-VERS-TLS1.3")
#+end_src

* Add package repositories

#+begin_src emacs-lisp :results output silent
  (require 'package)
  (setq package-enable-at-startup nil)
  (add-to-list 'package-archives '("melpa" . "https://melpa.org/packages/") t)
  (package-initialize)
#+end_src

* Garbage collection tuning

Thanks to [[https://github.com/abrochard/emacs-config/blob/master/configuration.org#garbage-collection-tuning][abrochard config]].

#+begin_src emacs-lisp :results output silent
  ;; When idle for 30sec run the GC no matter what.
  (defvar k-gc-timer
    (run-with-idle-timer 30 t (lambda () (garbage-collect))))

  ;; Set garbage collection threshold to 1GB.
  (setq gc-cons-threshold #x40000000)

  ;; Set garbage collection to 20% of heap
  (setq gc-cons-percentage 0.2)
#+end_src

* Automatic package installation via =use-package=

** Install =use-package= and dependencies

[[https://github.com/jwiegley/use-package][use-package]] is a macro that simplifies the syntax for installing and configuring packages.

[[https://github.com/myrjola/diminish.el][diminish]] hides minor-modes from the modeline. =use-package= has support for invoking =diminish= via the =:diminish= keyword.

#+begin_src emacs-lisp :results output silent
  (mapc
   (lambda (package)
     (unless (package-installed-p package)
       (progn
         (package-refresh-contents)
         (package-install package))))
   '(use-package diminish))
#+end_src

** Install =quelpa=

[[https://github.com/quelpa/quelpa][quelpa]] and [[https://github.com/quelpa/quelpa-use-package][quelpa-use-package]] allow installing packages from their source repositories if they are not available in an Emacs package registry

#+begin_src emacs-lisp :results output silent
  (unless (package-installed-p 'quelpa)
    (with-temp-buffer
      (url-insert-file-contents "https://raw.githubusercontent.com/quelpa/quelpa/master/quelpa.el")
      (eval-buffer)
      (quelpa-self-upgrade)
      (quelpa
       '(quelpa-use-package
         :fetcher git
         :url "https://github.com/quelpa/quelpa-use-package.git"))))
#+end_src

** Trigger =use-package=

=use-package-always-ensure= set to =t= is equivalent to all =use-package= declaractions having the =:ensure= keyword set to =t=.
=:ensure= causes packages to be installed automatically if not already present on system.

#+begin_src emacs-lisp :results output silent
  (require 'use-package)
  (require 'quelpa-use-package)
  (require 'diminish)

  (setq use-package-always-ensure t)
#+end_src


* Configure =exec-path-from-shell= (OS X)

[[https://github.com/purcell/exec-path-from-shell][exec-path-from-shell]] copies specified environment variables from your shell to Emacs.

On OS X in particular, Emacs launched as a GUI does not inherit all the shell env vars.

#+begin_src emacs-lisp :results output silent
  (setq exec-path-from-shell-arguments nil) ;; Suppress default arg to open interactive shell

  (use-package exec-path-from-shell
    :config (exec-path-from-shell-initialize))
#+end_src


* Install =better-defaults=

[[https://git.sr.ht/~technomancy/better-defaults][better-defaults]] provides a set of packages and configuration updates to add "better" defaults.
I've used this package for a bit and have grown accustomed to most of these defaults.

The main things I notably get from this are:
- Disable toolbar, menu bar and scroll bar
- =uniquify= library enabled and configured to add directory names to files if the names are not unique (default behavior is to append =<n>= to the end of file names)
- =show-paren-mode= enabled by default
- More extensive =apropros= searches
- =indent-tabs=mode= defaults to nil - spaces instead of tabs

Will maybe pull out these specific changes into dedicated config to avoid risk a new version of =better-defaults= changing some behavior I'm used to, but it should be fine.

#+begin_src emacs-lisp :results output silent
  (use-package better-defaults)
#+end_src

* UI / UX

** Startup UI / UX behavior

Inhibit startup message and have scratch buffer empty by default.

#+begin_src emacs-lisp :results output silent
  (setq initial-scratch-message nil)
  (setq inhibit-startup-message t)
#+end_src

** Emacs Dashboard

#+begin_src emacs-lisp :results output silent
    (use-package dashboard
      :config
      (progn
        (dashboard-setup-startup-hook)
        (setq dashboard-banner-logo-title "Welcome to the Coding Crypt"
              dashboard-startup-banner "/Users/will/OneDrive/media/pics/emacs-dashboard-mimikyu.png"
              dashboard-center-content t
              dashboard-projects-backend 'projectile
              dashboard-items '((projects . 10))
              dashboard-set-heading-icons t
              dashboard-set-file-icons t
              dashboard-set-footer nil
              dashboard-projects-switch-function 'counsel-projectile-switch-project-by-name)))
#+end_src

** General UI / UX tweaks

Do not make an audible noise when attempting to do something invalid.

#+begin_src emacs-lisp :results output silent
  (setq ring-bell-function 'ignore)
#+end_src

Ask for confirmation before quitting - remove pain of accidental =C-x C-c= fat fingers.

#+begin_src emacs-lisp :results output silent
  (setq confirm-kill-emacs 'y-or-n-p)
#+end_src

Accept =y= and =n= in place of =yes= and =no= respectively.

#+begin_src emacs-lisp :results output silent
  (fset 'yes-or-no-p 'y-or-n-p)
#+end_src

Auto-indent on =RET= in addition to usual =C-j=.

#+begin_src emacs-lisp :results output silent
  (define-key global-map (kbd "RET") 'newline-and-indent)
#+end_src

** Theme

The theme du jour will often change, but I tend to get them via [[https://github.com/hlissner/emacs-doom-themes][=emacs-doom-themes=]].

#+begin_src emacs-lisp :results output silent
  (use-package doom-themes
    :init (load-theme 'doom-tomorrow-night t))
#+end_src

** Font

FiraCode Nerd Font as font.

#+begin_src emacs-lisp :results output silent
  (defun willnevillain/set-default-font (font-height)
    (interactive)
    (progn
      (set-face-attribute 'default nil :font "FiraCode Nerd Font" :height font-height)
      (set-face-attribute 'fixed-pitch nil :font "FiraCode Nerd Font" :height font-height)))

  (willnevillain/set-default-font 128)
#+end_src

** Custom modeline

Using =doom-modeline= because it's minimal yet excellent out of the box. With =all-the-icons= support!

#+begin_src emacs-lisp :results output silent
  (use-package all-the-icons)
  ;; RUN THIS FIRST TIME (all-the-icons-install-fonts)
  ;; RUN THIS FIRST TIME (nerd-icons-install-fonts)

  (use-package doom-modeline
    :init (doom-modeline-mode 1)
    :custom ((doom-modeline-height 15)))
#+end_src

* Configure =ivy=

** Install =ivy=, rebind common functions, fuzzy match everywhere

#+begin_src emacs-lisp :results output silent
  (use-package counsel
    :config
    (setq-default counsel-rg-base-command
                  '("rg" "--max-columns" "240" "--with-filename" "--no-heading" "--line-number" "--color" "never" "--hidden" "%s"))
    :bind (("C-s" . swiper)
           ("M-x" . counsel-M-x)
           ("M-y" . counsel-yank-pop)
           ("C-x b" . ivy-switch-buffer)
           ("C-x C-b" . ivy-switch-buffer) ;; This key stroke is typically done in error trying to do C-x b
           ("C-x C-f" . counsel-find-file)
           ("C-c C-r" . ivy-resume)))

  (use-package ivy
    :diminish ivy-mode
    :config
    (ivy-mode t)
    (setq ivy-use-virtual-buffers t
          ivy-height 15
          ivy-count-format "(%d/%d) "
          ivy-use-selectable-prompt t
          ivy-re-builders-alist '((read-file-name-internal . ivy--regex-fuzzy)
                                  (t . ivy--regex-plus))))

  (use-package counsel-projectile
    :diminish counsel-projectile-mode
    :config
    (counsel-projectile-mode t)
    (define-key projectile-mode-map (kbd "C-c p") 'projectile-command-map))

  (use-package ivy-xref
    :config
    (setq xref-show-definitions-function #'ivy-xref-show-defs)
    (setq xref-show-xrefs-function #'ivy-xref-show-xrefs))

  (use-package wgrep)
#+end_src

* Configure =org-mode=

[[https://orgmode.org/][org-mode]] is a major mode that provides a plain text markup language - that doesn't sound that special in essence, but the ecosystem around org-mode is out of control.

You can manage novel planning, spreadsheets, personal and project TODO management, write research papers...whatever you want basically, and org-mode has a way to make it easier.

I use org-mode currently for daily task tracking and project management. I use =Todoist= for some critical timed / repeating tasks (e.g. =pay your credit card bills=).

** Core configuration

#+begin_src emacs-lisp :results output silent
  (use-package org

    :config
    (setq org-directory "~/OneDrive/org"
           org-log-repeat "time"
           org-deadline-warning-days 5)

    (require 'org-habit)
    (add-to-list 'org-modules 'org-habit)

    :bind
    (("C-c l" . org-store-link)
     ("C-c a" . org-agenda)
     ("C-c c" . org-capture)
     :map org-mode-map
     ("C-c C-q" . counsel-org-tag))

    :hook (org-mode . org-indent-mode))

  (defun willnevillain/org-find-file ()
    "Quickly open any org file in org-directory (non-recursive)."
    (interactive)
    (find-file (expand-file-name (ivy-read "Select file: " (directory-files org-directory nil "\.org$")) org-directory)))

  (global-set-key (kbd "C-c M-o") 'willnevillain/org-find-file)
#+end_src

** Configure agenda

Likewise, agenda files will live in OneDrive.

#+begin_src emacs-lisp :results output silent
  (setq org-agenda-skip-scheduled-if-done t
        org-agenda-files `(,org-directory)
        org-agenda-custom-commands '(("p" tags "PROJECT" nil)
                                     ("d" "Dashboard" ((agenda "" ((org-deadline-warning-days 7)))
                                                       (tags "+PROJECT+ACTIVE" ((org-agenda-overriding-header "Active Projects")))
                                                       (tags "+PROJECT-ACTIVE+LEVEL=1|+PROJECT-ACIVE+LEVEL=2" ((org-agenda-overriding-header "Inactive Projects")))))))

  (defun willnevillain/set-org-agenda-files ()
    "Utility function to refresh org-agenda-files recursively in org-directory if any new files have been added."
    (interactive)
    (setq org-agenda-files `(,org-directory)))
#+end_src

** Configure todo keywords

#+begin_src emacs-lisp :results output silent
  (setq org-todo-keywords
        '((sequence "TODO(t!)" "|" "DONE(d!)")))
#+end_src

** Configure additional export options

[[https://github.com/larstvei/ox-gfm][ox-gfm]] adds support for export org files to GitHub Flavored Markdown.

#+begin_src emacs-lisp :results output silent
  (use-package ox-gfm)
#+end_src

** Configure =org-babel= languages

[[https://orgmode.org/worg/org-contrib/babel/intro.html][org-babel]] allows executing src blocks in org files.

We define the languages that we want =org-babel= to execute.

#+begin_src emacs-lisp :results output silent
  (org-babel-do-load-languages 'org-babel-load-languages '((emacs-lisp . t)))
#+end_src

* Configure =projectile=

[[https://github.com/bbatsov/projectile][projectile]] is a very powerful library for interacting with multiple projects.
It provides features and functions that operate on the project level, such as jumping to a file in a project, jump to file at point in project, search in project, etc.

** Install =projectile= and core configuration

#+begin_src emacs-lisp :results output silent
  (use-package projectile
    :diminish projectile-mode
    :config
    (projectile-mode)
    (setq projectile-project-search-path '("~/code/digits")) ;; Specify dir(s) to search for projects
    (projectile-discover-projects-in-search-path) ;; Trigger project discovery
    (setq projectile-enable-caching t)) ;; Cache search results
#+end_src

* Configure =magit=

[[https://github.com/magit/magit][magit]] is a Git porcelain for Emacs, and honestly one of the killer apps of Emacs.

#+begin_src emacs-lisp :results output silent
  (use-package magit
    :bind ("C-x g" . magit-status))
#+end_src

[[https://github.com/alphapapa/magit-todos][magit-todos]] for visualizing =TODO= items marked across the source.

#+begin_src emacs-lisp :results output silent
  (use-package magit-todos
    :diminish magit-todos-mode
    :config (magit-todos-mode t))
#+end_src

* Configure =ace-window=

#+begin_src emacs-lisp :results output silent
  (use-package ace-window
    :bind ("M-o" . ace-window)
    :config (setq aw-keys '(?a ?s ?d ?f ?g)))
#+end_src

* Configure =treemacs=

#+begin_src emacs-lisp :results output silent
  (use-package treemacs
    :config
      (treemacs-follow-mode t)
      (treemacs-filewatch-mode t)
      (when treemacs-python-executable
        (treemacs-git-commit-diff-mode t))

      (pcase (cons (not (null (executable-find "git")))
                   (not (null treemacs-python-executable)))
        (`(t . t)
         (treemacs-git-mode 'deferred))
        (`(t . _)
         (treemacs-git-mode 'simple)))

      (treemacs-hide-gitignored-files-mode nil)
    :bind
    (:map global-map
          ("M-0"       . treemacs-select-window)
          ("C-x t 1"   . treemacs-delete-other-windows)
          ("C-x t t"   . treemacs)
          ("C-x t d"   . treemacs-select-directory)
          ("C-x t B"   . treemacs-bookmark)
          ("C-x t C-t" . treemacs-find-file)
          ("C-x t M-t" . treemacs-find-tag)))

  (use-package treemacs-projectile
    :after (treemacs projectile)
    :ensure t)

  (use-package treemacs-icons-dired
    :hook (dired-mode . treemacs-icons-dired-enable-once)
    :ensure t)

  (use-package treemacs-magit
    :after (treemacs magit)
    :ensure t)
#+end_src

* Programming environment

Anything related to a specific programming language, or programming major mode adjacent, lives here.

** Configure =flycheck=

[[https://github.com/flycheck/flycheck][flycheck]] provides on the fly syntax checking; it supports [[https://www.flycheck.org/en/latest/languages.html][many languages and checkers]].

#+begin_src emacs-lisp :results output silent
  (use-package flycheck
    :config
    (global-flycheck-mode))

  (use-package flycheck-projectile)
#+end_src

** Configure =editorconfig=

#+begin_src emacs-lisp :results output silent
  (use-package editorconfig
    :config (editorconfig-mode 1))
#+end_src

** Configure =company=

[[https://company-mode.github.io/][company]] is an in-buffer text completion framework; put more simply, when you're typing something, company will create a dropdown of possible options for what you're typing.

#+begin_src emacs-lisp :results output silent
  (use-package company
    :diminish company-mode
    :config
    (global-company-mode)
    (setq company-idle-delay 0.2 ;; show candidates 0.3 sec after idle from typing
          company-minimum-prefix-length 1 ;; show candidates as early as 1 character
          company-selection-wrap-around t ;; if you scroll past last/first candidate, wrap around
          global-company-modes '(not org-mode))) ;; disable company for modes
#+end_src

[[https://github.com/company-mode/company-quickhelp][company-quickhelp]] displays a pop-up when idling on a company candidate with documentation on the candidate.

#+begin_src emacs-lisp :results output silent
  (use-package company-quickhelp
    :config
    (setq company-quickhelp-delay 0.4)
    (company-quickhelp-mode))
#+end_src

** Configure =copilot=

[[https://github.com/copilot-emacs/copilot.el][copilot]] is the GitHub AI completion tool.

After installation, run =M-x copilot-install-server= and =M-x copilot-login= respectively.

#+begin_src emacs-lisp :results output silent
  ;; (use-package copilot
  ;;   :quelpa (copilot :fetcher git
  ;;                    :url "https://github.com/copilot-emacs/copilot.el.git")
  ;;   :hook (lsp-mode . copilot-mode)
  ;;   :bind
  ;;   (:map copilot-completion-map
  ;;         ("<tab>" . 'copilot-accept-completion)
  ;;         ("TAB" . 'copilot-accept-completion)
  ;;         ("C-n" . 'copilot-next-completion)
  ;;         ("C-p" . 'copilot-previous-completion))
  ;;   :config
  ;;   (add-to-list 'copilot-indentation-alist '(prog-mode 2)))
#+end_src


** LSP

*** Install and configure =lsp-mode=

#+begin_src emacs-lisp :results output silent
  (use-package lsp-mode
    :init
    (setq lsp-keymap-prefix "C-c l")

    :hook
    (lsp-mode . lsp-enable-which-key-integration)

    :bind
    (:map lsp-mode-map ("C-c l a c" . copilot-accept-completion))

    :config
    (setq lsp-file-watch-threshold 1500)

    :commands lsp)

  (use-package lsp-ui
    :commands lsp-ui-mode)

  (use-package lsp-ivy :commands lsp-ivy-workspace-symbol)
  (use-package which-key
    :diminish which-key-mode
    :config
    (which-key-mode))

  (use-package dap-mode
    :init
    (setq dap-auto-configure-features '(sessions locals controls tooltip)))

  (require 'dap-dlv-go)

  (advice-add 'lsp-execute-code-action :after (lambda (r) (call-interactively 'lsp)))
#+end_src

** Configure for =go=

=go-mode= and =gotest=

#+begin_src emacs-lisp :results output silent
  (use-package go-mode
    :bind ("C-c g t" . go-test-current-test))
  (add-to-list 'auto-mode-alist '("\\.go\\'" . go-mode))

  (add-hook 'go-mode-hook #'lsp-deferred)

  ;; Set up before-save hooks to format buffer and add/delete imports.
  ;; Make sure you don't have other gofmt/goimports hooks enabled.
  (defun lsp-go-install-save-hooks ()
    (add-hook 'before-save-hook #'lsp-format-buffer t t)
    (add-hook 'before-save-hook #'lsp-organize-imports t t))
  (add-hook 'go-mode-hook #'lsp-go-install-save-hooks)

  (use-package gotest)
#+end_src

** Configure development adjacent major modes

*** Install =yaml-mode=

[[https://github.com/yoshiki/yaml-mode][yaml-mode]] provides YAML file editing support.

#+begin_src emacs-lisp :results output silent
  (use-package yaml-mode)
#+end_src

*** Install =protobuf-mode=

#+begin_src emacs-lisp :results output silent
  (use-package protobuf-mode)
  (defun wvnix-editorconfig-format-before-save-hook ()
    (add-hook 'before-save-hook #'editorconfig-format-buffer))
  (add-hook 'protobuf-mode-hook #'wvnix-editorconfig-format-before-save-hook)
#+end_src

*** Install =graphql-mode=

#+begin_src emacs-lisp :results output silent
    (use-package graphql-mode
      :mode ("\\.graphql\\'" . graphql-mode))
#+end_src
*** Install =markdown-mode=

[[https://github.com/jrblevin/markdown-mode][markdown-mode]] provides Markdown editing support.

#+begin_src emacs-lisp :results output silent
  (use-package markdown-mode
    :mode (("README\\.md\\'" . gfm-mode)
           ("\\.md\\'" . markdown-mode)))
#+end_src

*** Install =json-mode=

[[https://github.com/joshwnj/json-mode][json-mode]] provides JSON file editing support.

#+begin_src emacs-lisp :results output silent
  (use-package json-mode
    :hook (json-mode . (lambda ()
              (make-local-variable 'js-indent-level)
              (setq js-indent-level 2))))
#+end_src

*** Install =dockerfile-mode=

[[https://github.com/spotify/dockerfile-mode][dockerfile-mode]] provides Dockerfile editing support (and image building as well).
Somewhat of a surprise to me, this is maintained by Spotify.

#+begin_src emacs-lisp :results output silent
  (use-package dockerfile-mode)
#+end_src

** Configure programming related UI packages

*** Install =git-gutter=

[[https://github.com/emacsorphanage/git-gutter][git-gutter]] provides visual indicators in the left gutter of a buffer of git changes.

#+begin_src emacs-lisp :results output silent
  (use-package git-gutter
    :diminish git-gutter-mode
    :config (global-git-gutter-mode 1))
#+End_src

* Infrastructure and application management

Anything related to connecting to, declaring configuration for or otherwise managing live infrastructure or applications.

** Configure =kubel=

[[https://github.com/abrochard/kubel][kubel]] allows controlling Kubernetes with limited permissions through Emacs.

The [[https://github.com/abrochard][author]] gave an interesting talk about how the extension was born and built, which I encourage you to watch [[https://www.youtube.com/watch?v=w3krYEeqnyk][here]].

#+begin_src emacs-lisp :results output silent
  (use-package kubel)
#+end_src

** Configure =k8s-mode=

[[https://github.com/TxGVNN/emacs-k8s-mode][k8s-mode]] is a major mode for editing K8s YAML files.

#+begin_src emacs-lisp :results output silent
  (use-package k8s-mode)
#+end_src

* Configure =restclient-mode=

#+begin_src emacs-lisp :results output silent
  (use-package restclient
    :mode (("\\.http\\'" . restclient-mode)))
#+end_src

* Configure =vterm=

#+begin_src emacs-lisp :results output silent
  (use-package vterm
    :bind ("C-c t" . vterm))
#+end_src

* Configure =eshell=

[[https://www.gnu.org/software/emacs/manual/html_mono/eshell.html][eshell]] is a shell-like command interpreter implemented entirely in Emacs Lisp.

=eshell= is an interesting beast that I won't say I've come close to taming.

Recommended reading / viewing to demystify =eshell=:
- [[https://masteringemacs.org/article/complete-guide-mastering-eshell][Mastering Emacs article "Mastering Eshell"]]
- [[https://www.youtube.com/watch?v=RhYNu6i_uY4][Howard Abrams London Emacs Meetup talk "Introduction to EShell"]]

** Package configuration

#+begin_src emacs-lisp :results output silent
  (use-package eshell
    :config
    (setq eshell-scroll-to-bottom-on-input 'all ;; Scroll to bottom of buffer when entering input
          eshell-error-if-no-glob t ;; Error if glob pattern does not match
          eshell-hist-ignoredups t ;; ???, but a lot of people have it
          eshell-save-history-on-exit t ;; Save history of eshell process on exist
          eshell-prefer-lisp-functions nil ;; Prefer external commands to Lisp functions
          eshell-destroy-buffer-when-process-dies t) ;; When eshell process exists, destroy buffer
    (add-hook 'eshell-mode-hook
            (lambda ()
              (define-key eshell-mode-map (kbd "C-r") 'counsel-esh-history))))

#+end_src

** Utility functions

Defined functions prefaced with =eshell/= should be invokable using everything after the =/= in eshell. 

#+begin_src emacs-lisp :results output silent
  (defun eshell/clear ()
    "Clear eshell buffer."
    (let ((inhibit-read-only t))
      (erase-buffer)))

  (defun eshell/close ()
    "Close eshell window."
    (delete-window))
#+end_src

=eshell= related interactive functions defined here.

#+begin_src emacs-lisp :results output silent
  (defun eshell-here (arg)
    "Opens a new eshell buffer in the lower quarter of the frame.
  If called without \\[universal-argument], it opens the shell in the project root directory.
  If called with \\[universal-argument], it opens the shell in the current buffer's directory.
  The eshell is renamed to match the directory to make multiple eshell windows easier.
  Projectile is a required to determine the project root."
    (interactive "P")
    (let* ((height (/ (window-total-height) 4)))
      (split-window-vertically (- height))
      (other-window 1)
      (if (and (equal arg nil)
               (fboundp 'projectile-project-root)
               (projectile-project-root))
          (let ((default-directory (projectile-project-root))
                (eshell-buffer-name (concat "*eshell <" (projectile-project-name) ">*")))
            (eshell "new"))
        (let ((eshell-buffer-name (concat "*eshell <" (buffer-file-name) ">*")))
          (eshell "new")))))

  (global-set-key (kbd "C-!") 'eshell-here)
#+end_src

Miscellaneous non-interactive and non-=eshell/= utility functions defined below.

#+begin_src emacs-lisp :results output silent
  (defun eshell-pop--kill-and-delete-window ()
    "Used on eshell exit hook, will delete the window if not the only one in the frame"
    (unless (one-window-p)
      (delete-window)))

  (add-hook 'eshell-exit-hook 'eshell-pop--kill-and-delete-window)
#+end_src

** Visual command and subcommand definitions

=eshell= is not a fully functioning terminal, and cannot handle certain visual commands.

#+begin_src emacs-lisp :results output silent
  (setq eshell-visual-commands '("htop" "top" "less" "more" "screen" "vi" "vim"))
  (setq eshell-visual-subcommands '("git" "log" "diff" "show" "ssh"))
#+end_src

Related: set Unix Pager to be the =cat= command.

#+begin_src emacs-lisp :results output silent
  (setenv "PAGER" "cat")
#+end_src

* Configure =yasnippet=

[[https://github.com/joaotavora/yasnippet][yasnippet]] is a template and expansion system for Emacs.

Snippets from the [[https://github.com/AndreaCrotti/yasnippet-snippets][yasnippet-snippets community library]] are loaded alongside my own defined snippets.

My snippets are located at =~/.emacs.d/snippets=, community library at =~/.emacs.d/yasnippet-snippets=.

#+begin_src emacs-lisp :results output silent
  (use-package yasnippet
    :diminish yas-minor-mode
    :config
    (add-to-list 'yas-snippet-dirs "~/.emacs.d/snippets")
    (add-to-list 'yas-snippet-dirs "~/.emacs.d/yasnippet-snippets")
    (yas-global-mode)
    (global-set-key (kbd "M-/") 'company-yasnippet))
#+end_src

* Configure blogging tool(s)

[[https://github.com/masasam/emacs-easy-hugo][easy-hugo]] is a package that makes it easier to work with the [[https://gohugo.io/][hugo]] static site generator.

#+begin_src emacs-lisp :results output silent
  (use-package easy-hugo
    :init
    (setq easy-hugo-basedir "~/code/blog/")
    (setq easy-hugo-url "https://willnevillain.github.io")
    (setq easy-hugo-postdir "content/blog")
    (setq easy-hugo-default-ext ".org"))
#+end_src

* Global utility functions

Any utility functions that are used at a global level go here.

** Jump to this configuration file

#+begin_src emacs-lisp :results output silent
  (defun willnevillain/goto-configuration ()
    "Go to org configuration file."
    (interactive)
    (find-file "~/.emacs.d/config.org"))

  (global-set-key (kbd "C-c M-c") 'willnevillain/goto-configuration)
#+end_src

** Toggle font size for streaming mode

This function is pretty wild and probably horrible but it works - used for toggling font size when doing coding streams.

#+begin_src emacs-lisp :results output silent
  (defun willnevillain/toggle-stream-fontsize ()
    "Toggle the font size between default and large for streaming"
    (interactive)
    (if (equal (boundp 'willnevillain/toggle-stream-fontsize--embiggened) nil)
        (setq willnevillain/toggle-stream-fontsize--embiggened nil))
    (if willnevillain/toggle-stream-fontsize--embiggened
        (progn
          (willnevillain/set-default-font 128)
          (setq willnevillain/toggle-stream-fontsize--embiggened nil))
      (progn
        (willnevillain/set-default-font 200)
        (setq willnevillain/toggle-stream-fontsize--embiggened t))))
#+end_src

** Get keys of hash table sorted alphabetically

#+begin_src emacs-lisp :results output silent
  (defun willnevillain/sorted-hash-table-keys (hash-table)
    (let ((keys ()))
      (maphash (lambda (k v) (push k keys)) hash-table)
      (sort keys
            (lambda (k1 k2)
              (string-lessp k1 k2)))))
#+end_src

* Miscellaney

Configuration that doesn't really fit elsewhere or deserve a top level heading go here.

** Configure =helpful= for better Emacs docs

[[https://github.com/Wilfred/helpful][helpful]] provides much more context and detail in the help buffer.

We rebind all the command help keybinds to their =helpful= equivalent.

#+begin_src emacs-lisp :results output silent
  (use-package helpful
    :bind (("C-h f" . helpful-callable)
           ("C-h v" . helpful-variable)
           ("C-h k" . helpful-key)
           ("C-h F" . helpful-function)
           ("C-h C" . helpful-command)))
#+end_src

** Configure backups to go into one folder

I dislike polluting the file tree with Emacs backups - I put them all in =~/.emacs.d/backups=.

#+begin_src emacs-lisp :results output silent
  (setq backup-directory-alist '(("." . "~/.emacs.d/backups")))
#+end_src

** Configure =flyspell= spell checking

[[https://www.gnu.org/software/emacs/manual/html_node/emacs/Spelling.html][flyspell]] (built in) will highlight misspelled words by way of an underlying spell checking program (on OS X it is probably =ispell=).

I only want global spell checking in =org-mode= buffers, and for programming mode buffers enable =flyspell-prog-mode= which only spell checks comments and strings.

#+begin_src emacs-lisp :results output silent
  (add-hook 'org-mode-hook 'flyspell-mode)
#+end_src

** Configure diminished minor modes

There are a few minor modes not specifically configured in this file that I like to diminish.

#+begin_src emacs-lisp :results output silent
  (diminish 'abbrev-mode)
  (diminish 'auto-revert-mode)
  (diminish 'eldoc-mode)
  (diminish 'flyspell-mode)
#+end_src

** Configure =csv-mode=

#+begin_src emacs-lisp :results output silent
  (use-package csv-mode)
#+end_src

* Configure and load custom file

By default, Emacs places anything customized via =customize= at the bottom of =init.el=.

Prefer to place all this cruft in a specific file.

#+begin_src emacs-lisp :results output silent
  (setq custom-file
        (expand-file-name "custom.el" user-emacs-directory))
  (if (file-exists-p custom-file) (load custom-file))
#+end_src
